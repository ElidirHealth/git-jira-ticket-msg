#!/usr/bin/env perl
require Term::ANSIColor;
use Term::ANSIColor qw(:constants);

my $file = shift;

if (`git symbolic-ref HEAD` =~ /trivial-/) {
    exit 0;
}

(my @projects = split /\n/, `git config --get-all jira.project`)
    or die "Please set an jira.project like so:\n\n    git config --add jira.project MYPROJ\n\n(note: you can do this multiple times. Add --global to configure globally)\n";

my $project_re = join '|', @projects;
my $re = qr/($project_re)-\d+/i;

open my $handle, '<', $file or die "git error? $!";
my $first_line = <$handle>;

if ($first_line =~ $re) {
    exit 0; # OK!
}
elsif ($first_line =~ /^Merge /) {
    exit 0; # merges don't need a ticket number
}
elsif ($first_line =~ /\bWIP\b/) {
    exit 0; # commit will be rewritten later
}
elsif ($first_line =~ /^\s*$/) {
    exit 0; # let git generate the "Aborted" message
}
else {
    die RED, "Aborted! Your commit message's first line must reference a ticket from projects " . join (' or ', map { "$_" } @projects), RESET, "\n";    
}
